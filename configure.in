dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(gcalctool, 5.7.0)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

AC_PROG_INTLTOOL

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

PKG_CHECK_MODULES(ATK, atk >= 1.5)
AC_SUBST(ATK_CFLAGS)
AC_SUBST(ATK_LIBS)

pkg_modules="gtk+-2.0 >= 2.6 gobject-2.0 gdk-x11-2.0 glib-2.0 atk gdk-pixbuf-2.0"
PKG_CHECK_MODULES(PACKAGE, [$pkg_modules])
AC_SUBST(PACKAGE_CFLAGS)
AC_SUBST(PACKAGE_LIBS)

LIBGNOME_REQUIRED=2.0.0
LIBGNOMEUI_REQUIRED=2.0.0

AM_PROG_LEX
if [[ "$LEX" != "flex" ]]; then
	AC_MSG_ERROR(flex is required to create the gcalctool scanners)
fi
LFLAGS="-8"
AC_SUBST(LFLAGS)

AC_PROG_YACC
dnl determine if a usable yacc is available on this system
AC_CHECK_PROG(HAVE_YACC, $YACC, yes, no)
if [[ "$HAVE_YACC" = "no" ]]; then
	AC_MSG_ERROR($YACC is not usable as yacc - consider using bison)
fi

dnl ###########################################################################
dnl Disable GNOME support
dnl ###########################################################################
AC_ARG_ENABLE(gnome,
        [  --disable-gnome        Remove GNOME support. Don't do this, it removes essential features],
        disable_gnome=yes, disable_gnome=no)

AC_MSG_CHECKING(whether GNOME support must be compiled in)
if test x"${disable_gnome}" = xyes; then
   AC_MSG_RESULT([no])
else
   AC_MSG_RESULT([yes])
fi
AM_CONDITIONAL(DISABLE_GNOME, test x"${disable_gnome}" = xyes)


dnl ###########################################################################
dnl PKG_CONFIG and required versions
dnl ###########################################################################
if test x"${disable_gnome}" = xyes; then
GNOME_UTIL_CFLAGS="-DDISABLE_GNOME"
AC_DEFINE(DISABLE_GNOME, 1, [ Remove GNOME support. ])
else
PKG_CHECK_MODULES(GNOME_UTILS, libgnome-2.0 >= $LIBGNOME_REQUIRED libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED)
GNOME_UTIL_CFLAGS="$GNOME_UTIL_CFLAGS"
GNOME_UTIL_LIBS="$GNOME_UTIL_LIBS"
fi
AC_SUBST(GNOME_UTILS_CFLAGS)
AC_SUBST(GNOME_UTILS_LIBS)


PKG_CHECK_MODULES(GCONF, gconf-2.0 >= 1.1.9)
AC_SUBST(GCONF_CFLAGS)
AC_SUBST(GCONF_LIBS)

dnl ********************
dnl scrollkeeper checks
dnl ********************

AC_PATH_PROG(SCROLLKEEPER_CONFIG, scrollkeeper-config,no)
if test x$SCROLLKEEPER_CONFIG = xno; then
  AC_MSG_ERROR(Couldn't find scrollkeeper-config. Please install the scrollkeeper package)
fi

dnl *******************************
dnl Internationalization
dnl *******************************

GETTEXT_PACKAGE=gcalctool
AC_PREREQ(2.53)
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE")

dnl Add the languages which your application supports here.
AM_PROG_LIBTOOL
ALL_LINGUAS="am ar az be bg bs ca cs cy da de el en_CA en_GB en_US es et eu fa fi fr ga gl gu he hi hr hu id it ja ko lt mk ml mn ms nb ne nl no or pa pl pt pt_BR ro ru rw sk sl sq sr sr@Latn sv ta te th tk tr uk vi xh zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT

dnl Use -Wall if we have gcc.
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

dnl Checking for gconftool-2
AC_PATH_PROG(GCONFTOOL, gconftool-2, no)

if test "x$GCONFTOOL" = xno; then
  AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi

AM_GCONF_SOURCE_2

AC_OUTPUT([
Makefile
gcalctool/Makefile
po/Makefile.in
help/Makefile
help/C/Makefile
help/de/Makefile
help/es/Makefile
help/fr/Makefile
help/it/Makefile
help/ja/Makefile
help/ko/Makefile
help/sv/Makefile
help/zh_CN/Makefile
help/zh_HK/Makefile
help/zh_TW/Makefile
])
